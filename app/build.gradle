plugins {
    id 'com.android.application'
    id 'com.gladed.androidgitversion' version '0.4.4'

    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

androidGitVersion {
    prefix 'v'  // to match our tag format, e.g. v0.0.1-beta
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "saarland.cispa.artist.artistgui"
        minSdkVersion 23
        targetSdkVersion 28
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

task artistGuiVersion {
    doLast {

        def git_commit = ['sh', '-c', 'git log -1 | grep commit'].execute().text.trim()
        def git_status = ['sh', '-c', 'git status --porcelain'].execute().text.trim()
        ext.versionfile = new File('app/src/main/assets/VERSION_ARTISTGUI.md')

        println git_commit
        println git_status

        versionfile.text = git_commit + '\n' + git_status
    }
}

assemble.dependsOn 'artistGuiVersion'

ext {
    supportLibVersion = '28.0.0'
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    hamcrestVersion = '1.3'
}

dependencies {
    implementation project(':dexterous')

    implementation 'androidx.appcompat:appcompat:1.1.0-alpha01'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'com.google.android.material:material:1.1.0-alpha02'

    implementation "androidx.room:room-runtime:2.1.0-alpha03"
    kapt "androidx.room:room-compiler:2.1.0-alpha03"

    // Support library depends on this lightweight import
    implementation "androidx.lifecycle:lifecycle-runtime:2.1.0-alpha01"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-all:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
