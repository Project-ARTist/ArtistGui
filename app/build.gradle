plugins {
    id 'com.android.application'
    id 'com.gladed.androidgitversion' version '0.4.4'

    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-android-extensions'
}

androidGitVersion {
    prefix 'v'  // to match our tag format, e.g. v0.0.1-beta
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

//    splits {
//        abi {
//            enable true
//            reset()
//            include 'armeabi', 'armeabi-v7a',  'arm64-v8a', 'x86', 'x86_64'
//            universalApk true
//        }
//    }

    defaultConfig {
        applicationId "saarland.cispa.artist.artistgui"
        minSdkVersion 23
        targetSdkVersion 28
        versionName androidGitVersion.name()
        versionCode androidGitVersion.code()

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    lintOptions {
        // set to true to turn off analysis progress reporting by lint
        quiet false
        // if true, stop the gradle build if errors are found
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
        checkReleaseBuilds false
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
//    sourceSets {
//        main {
//            jniLibs.srcDir 'src/main/libs' //set .so files location to libs
//            jni.srcDirs = [] //disable automatic ndk-build call
//        }
//    }
}

task artistGuiVersion {
    doLast {

        def git_commit = ['sh', '-c', 'git log -1 | grep commit'].execute().text.trim()
        def git_status = ['sh', '-c', 'git status --porcelain'].execute().text.trim()
        ext.versionfile = new File('app/src/main/assets/VERSION_ARTISTGUI.md')

        println git_commit
        println git_status

        versionfile.text = git_commit + '\n' + git_status
    }
}

assemble.dependsOn 'artistGuiVersion'

ext {
    supportLibVersion = '28.0.0'
    junitVersion = '4.12'
    mockitoVersion = '1.10.19'
    hamcrestVersion = '1.3'
}

dependencies {
    implementation project(':dexterous')

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:support-v13:$supportLibVersion"

    implementation "android.arch.persistence.room:runtime:1.1.1"
    kapt "android.arch.persistence.room:compiler:1.1.1"

    // Support library depends on this lightweight import
    implementation "android.arch.lifecycle:runtime:1.1.1"

    implementation 'com.madgag.spongycastle:core:1.54.0.0'
    implementation 'com.madgag.spongycastle:pkix:1.54.0.0'

    // Dependencies for local unit tests
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-all:$mockitoVersion"
    testImplementation "org.hamcrest:hamcrest-all:$hamcrestVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
